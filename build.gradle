/*
    ./gradlew clean executableCapsule
    build/libs/fregec-1.0-SNAPSHOT-capsule.jar -d fregebuild helloworld.fr
    ll fregebuild
    
*/

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "com.github.jengelman.gradle.plugins:shadow:1.1.2"
  }
}

plugins {
  id "us.kirchmeier.capsule" version "0.9.0"
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'java'
apply plugin: 'application'

group = 'org.svenehrke.TTT'
version = '1.0-SNAPSHOT'

mainClassName = 'frege.compiler.Main'

repositories {
    jcenter()
    flatDir name: 'localRepository', dirs: '0_localrepository'
}


task fatCapsule(type: FatCapsule) {
  applicationClass 'frege.compiler.Main'    
}
task executableCapsule(type:FatCapsule){
  applicationClass 'frege.compiler.Main'
  reallyExecutable {
    script file('xjar_script.sh')
  }
}
configurations {
	fregec
}
dependencies {
	compile files('0_localrepository/frege3.21.586-g026e8d7.jar')
	fregec files('0_localrepository/frege3.21.586-g026e8d7.jar')
	testCompile 'junit:junit:4.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
// java -Xss1m -jar build/libs/fregec-1.0-SNAPSHOT-capsule.jar -d fregebuild helloworld.fr
// java -Xss1m -jar fregec.jar -d build test.fr
task fregec(type: JavaExec) {
    classpath configurations.fregec
    main = "frege.compiler.Main"
    jvmArgs = ['-Xss1m']
    args "-d", 'build', 'helloworld.fr'
}

//java -classpath 0_localrepository/frege3.21.586-g026e8d7.jar:fregebuild Main
task frege(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath + files('build')
    main = "Main"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

sourceCompatibility = 1.7
//targetCompatibility = 1.7
